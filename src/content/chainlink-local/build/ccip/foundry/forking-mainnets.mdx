---
section: chainlinkLocal
date: Last Modified
title: "Using the CCIP Local Simulator to fork mainnets"
isIndex: false
---

import Common from "@features/chainlink-local/Common.astro"
import { Aside } from "@components"

<Common callout="importPackage" />

When working in forked environments, we rely on the [`Register.sol`](/chainlink-local/api-reference/solidity/ccip/register) smart contract to speed up the process of simulating cross-chain transactions in Foundry.

It has latest details for most of test networks, however intentionaly there are no main network details.

<Aside type="note" title="Suggested Best Practice">
  Even for supported networks, the best practices is to always verify contract addresses from the
  [`Register.sol`](/chainlink-local/api-reference/solidity/ccip/register) smart contract against ones on the [CCIP
  Directory](ccip/directory/mainnet).

For example:

```solidity
pragma solidity ^0.8.0;

import { Test, console } from "forge-std/Test.sol"
import {
    CCIPLocalSimulatorFork,
    Register
} from "@chainlink/local/src/ccip/CCIPLocalSimulatorFork.sol"

contract ExampleTest is Test {
    CCIPLocalSimulatorFork public ccipLocalSimulatorFork;

    uint256 public ethSepoliaFork;
    Register.NetworkDetails public ethSepoliaNetworkDetails;

    function setUp() public {
        string memory SEPOLIA_RPC_URL = vm.envString("SEPOLIA_RPC_URL");
        ethSepoliaFork = vm.createFork(SEPOLIA_RPC_URL);

        ccipLocalSimulatorFork = new CCIPLocalSimulatorFork();
        vm.makePersistent(address(ccipLocalSimulatorFork));

        vm.selectFork(ethSepoliaFork);
        assertEq(block.chainid, 11155111);

        ethSepoliaNetworkDetails =
            ccipLocalSimulatorFork.getNetworkDetails(block.chainid);
        assertEq(
            ethSepoliaNetworkDetails.chainSelector,
            16015286601757825753
        );
        assertEq(
            ethSepoliaNetworkDetails.routerAddress,
            0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59
        );
    }
}
```

</Aside>

## Forking mainnets

Chainlink Local is designed to work in all forked environments where CCIP contracts exist in the forked block. Main networks details are not included in the [`Register.sol`](/chainlink-local/api-reference/solidity/ccip/register) smart contract, rather it is developers' responsibility to provide those details.

Imagine that you are writing a simple test to simulate a transfer of cross-chain message from Ethereum Mainnet to Polygon Mainnet in a forked environment in Foundry.

You can write something like this, for example:

```solidity
pragma solidity ^0.8.0;

import { Test, console } from "forge-std/Test.sol"
import { CCIPLocalSimulatorFork, Register } from "@chainlink/local/src/ccip/CCIPLocalSimulatorFork.sol"

import { IRouterClient } from "@chainlink/contracts-ccip/src/v0.8/ccip/interfaces/IRouterClient.sol";
import { Client } from "@chainlink/contracts-ccip/src/v0.8/ccip/libraries/Client.sol";

contract ExampleTest is Test {
    CCIPLocalSimulatorFork public ccipLocalSimulatorFork;

    uint256 public ethereumMainnetForkId;
    uint256 public polygonMainnetForkId;

    Register.NetworkDetails public ethSepoliaNetworkDetails;

    function setUp() public {
        string memory ETHEREUM_MAINNET_RPC_URL = vm.envString("ETHEREUM_MAINNET_RPC_URL");
        string memory POLYGON_MAINNET_RPC_URL = vm.envString("POLYGON_MAINNET_RPC_URL");
        ethereumMainnetForkId = vm.createFork(ETHEREUM_MAINNET_RPC_URL);
        polygonMainnetForkId = vm.createFork(POLYGON_MAINNET_RPC_URL);

        ccipLocalSimulatorFork = new CCIPLocalSimulatorFork();
        vm.makePersistent(address(ccipLocalSimulatorFork));
    }

    function test_example() public {
        // select ethereum fork
        vm.selectFork(ethereumMainnetForkId);

        address alice = makeAddr("alice");
        vm.deal(alice, 1 ether);

        vm.startPrank(alice);
        Client.EVM2AnyMessage memory message = Client.EVM2AnyMessage({
            receiver: abi.encode(ccipReceiverAddress),
            data: abi.encode("Hello world"),
            tokenAmounts: new Client.EVMTokenAmount[](0),
            extraArgs: "",
            feeToken: address(0)
        });

        address ethereumMainnetCcipRouterAddress = 0x80226fc0Ee2b096224EeAc085Bb9a8cba1146f7D;
        uint64 polygonMainnetChainSelector = 4051577828743386545;
        IRouterClient(ethereumMainnetCcipRouterAddress).ccipSend(polygonMainnetChainSelector, message);
        vm.stopPrank();

        ccipLocalSimulatorFork.switchChainAndRouteMessage(polygonMainnetForkId);
    }
}
```

If you run the `forge test` command the **test will fail**, because there are no Polygon Mainnet network details present in the [`Register.sol`](/chainlink-local/api-reference/solidity/ccip/register) smart contract.

The only thing needed to fix it, is to call the `setNetworkDetails` function of the [`Register.sol`](/chainlink-local/api-reference/solidity/ccip/register) smart contract.

```solidity
pragma solidity ^0.8.0;

import { Test, console } from "forge-std/Test.sol"
import { CCIPLocalSimulatorFork, Register } from "@chainlink/local/src/ccip/CCIPLocalSimulatorFork.sol"

contract ExampleTest is Test {
    function setUp() public {
        // Code from previous section goes here...

        // select polygon fork
        vm.selectFork(polygonMainnetForkId);
        assertEq(block.chainid, 137);

        uint64 polygonMainnetChainSelector = 4051577828743386545;
        address polygonMainnetCcipRouterAddress = 0x849c5ED5a80F5B408Dd4969b78c2C8fdf0565Bfe;

        ccipLocalSimulatorFork.setNetworkDetails(
            block.chainid,
            Register.NetworkDetails({
                chainSelector: polygonMainnetChainSelector,
                routerAddress: polygonMainnetCcipRouterAddress,
                linkAddress: address(0), // not needed for this test
                wrappedNativeAddress: address(0), // not needed for this test
                ccipBnMAddress: address(0), // not needed for this test
                ccipLnMAddress: address(0), // not needed for this test
                rmnProxyAddress: address(0), // not needed for this test
                registryModuleOwnerCustomAddress: address(0), // not needed for this test
                tokenAdminRegistryAddress: address(0) // not needed for this test
            })
        );
    }
}
```
